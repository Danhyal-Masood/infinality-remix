# Maintainer: Philip Deljanov <philip dot deljanov at gmail dot com>
# Contributor: bohoomil <bohoomil at zoho dot com>

pkgname=lib32-fontconfig-infinality-remix
_basename=fontconfig
pkgdesc="A library for configuring and customizing font access, optimized for Infinality Remix."
url='http://www.fontconfig.org/release/'
license=('custom' 'MIT')
pkgver=2.13.91+24+g75eadca
pkgrel=2
arch=(i686 x86_64)
groups=('infinality-remix')

depends=('lib32-expat' 'freetype2-infinality-remix')
makedepends=('git' 'autoconf-archive' 'gperf' 'python-lxml' 'python-six' 'docbook-utils' 'docbook-sgml'
             'perl-sgmls' 'texlive-formatsextra>=2017' 'lynx' 'lib32-json-c')

options=('libtool')

provides=("lib32-fontconfig=$pkgver")

conflicts=('lib32-fontconfig'
            'lib32-fontconfig-infinality'
            'lib32-fontconfig-infinality-git'
            'lib32-fontconfig-infinality-ultimate'
            'lib32-fontconfig-infinality-ultimate-git'
            'lib32-fontconfig-iu-comp')



install=lib32-fontconfig-infinality-remix.install

# Arch official upstream version commit
_commit=75eadca26648abf69497691ff0f4c7803b9ff23c

# Infinality Remix fontconfig configuration version
_remixver=20191030

source=("git+https://anongit.freedesktop.org/git/fontconfig#commit=$_commit"
        fontconfig-infinality-remix-srcs-${_remixver}.tar.bz2
        fc-presets
        lib32-fc-cache-infinality-remix.hook
        0001-configure-${pkgver}.patch
        0002-configure-${pkgver}.ac.patch
        0003-Makefile-${pkgver}.in.patch
        0004-Makefile-${pkgver}.conf.d.patch
        0005-Makefile-${pkgver}.am.in.patch)

sha256sums=('SKIP'
            '6aedf617e282c7c001c47828d710e1fa44bc3e1fe4ee4e79e461d5d58cc19182'
            '23f70387eec20761b8b89f7a312f2859e4bfedd07325f3a1de12f86cca094711'
            '45d82b88932ca4e870874e9cb0a5fac9d99585780fd82f0fa01517329b222fb8'
            '45a1ea897f0c7687f4ba1d2c200b17145cc5246e4a82eaa33e09d1b843030cb7'
            '006a14e00017736813002034996b024bb7133bd3eeda6b7f74e4c2880b64031d'
            '7dfc5ba9b20e0326e6a3a880f18d58a7ed31f7af025627670f210ec109be1a81'
            'fd3487482109874d21c0e537284920392f93c4362b70a765de376c66654edec6'
            'f989fce1a1537f6e9ad42b8a2d1bb87a451af6b4fb80f9281f8773985ce85627')

pkgver() {
    cd fontconfig
    git describe --tags | sed 's/-/+/g'
}

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
prepare() {
    patches=(0001-configure-${pkgver}.patch
            0002-configure-${pkgver}.ac.patch
            0003-Makefile-${pkgver}.in.patch
            0004-Makefile-${pkgver}.conf.d.patch
            0005-Makefile-${pkgver}.am.in.patch)

    # Copy the Infinality font configuration.
    cd "${_basename}-infinality-remix-srcs"
    cp -r conf/conf.d.infinality "${srcdir}/${_basename}/conf.d.infinality"

    cd "${srcdir}/${_basename}"
    NOCONFIGURE=1 ./autogen.sh

    # Infinality & other downstream patches
    for patch in "${patches[@]}"; do
        patch -Np1 -i ../${patch}
    done

    aclocal
    libtoolize -f
    automake -afi
}

build() {
    export CC="gcc -m32"
    export CXX="g++ -m32"
    export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"
    cd "${_basename}"

    ./configure --prefix=/usr \
        --libdir=/usr/lib32 \
        --sysconfdir=/etc \
        --with-templatedir=/etc/fonts/conf.avail \
        --with-templateinfdir=/etc/fonts/conf.avail.infinality \
        --with-xmldir=/etc/fonts \
        --localstatedir=/var \
        --disable-static \
        --with-default-fonts=/usr/share/fonts \
        --with-add-fonts=/usr/share/fonts
    make
}

check() {
 cd "${_basename}"
 make -k check
}

package() {
    cd "${_basename}"
    make DESTDIR="${pkgdir}" install

    # Install license
    install -m755 -d "${pkgdir}"/usr/share/licenses/"${_basename}"
    install -m644 COPYING "${pkgdir}"/usr/share/licenses/"${_basename}"
    rm -r "$pkgdir"/{etc,usr/{include,share}}

    mv "$pkgdir"/usr/bin/fc-cache{,-32}
    find "$pkgdir/usr/bin" -type f -not -name '*-32' -delete
    # Install fc-presets
#    install -m755 "${srcdir}"/fc-presets-32 "${pkgdir}"/usr/bin/"fc-presets-32"

    # ALPM/Pacman hook
    install -D -m 644 "${srcdir}"/lib32-fc-cache-infinality-remix.hook \
        "${pkgdir}/usr/share/libalpm/hooks/90-lib32-fc-cache-infinality-remix.hook"


}
